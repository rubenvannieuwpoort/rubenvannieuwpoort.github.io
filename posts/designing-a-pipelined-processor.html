<!DOCTYPE html>
<html lang="en">
<head>
<meta charset="utf-8">
<meta name="description" content="I lay down a template which can be used in the design of a pipelined processor.">
<title>Designing a pipelined processor</title>
<meta name="viewport" content="width=device-width">
<link rel="stylesheet" type="text/css" href="style.css"> <link rel="stylesheet" type="text/css" href="katex.min.css">

<link rel="shortcut icon" type="image/png" href="../favicon.png">
</head>
<body>
<article><h1>Designing a pipelined processor</h1>
<p>I have been working on a simple in-order pipelined processor for a few months now. While there’s still a lot to do, things are (finally) coming together nicely. One of the most difficult parts in the design process was to get the pipelining logic right. So I decided to document the pipelining logic for myself. Maybe it is useful for someone else as well? The template I made can be found <a href="https://github.com/rubenvannieuwpoort/stage_template">on github</a>.</p>
<p>Pretty much any pipelined processor will need to handle <em>pipeline stalls</em>. In case of a pipeline stall a stage cannot process data coming from the previous stage, and needs to signal this to the previous stage. However, for speed, all stages are <em>synchronous</em>, meaning that the output in clock cycle N depends on the state in clock cycle N - 1. So, if a stage can’t process the output of the previous stage in clock cycle N, it can only tell the previous stage in clock cycle N + 1. But in clock cycle N + 1, the previous stage has already pushed new output. The essential idea is that this new output needs to be buffered by the current stage.</p>
<p>That’s essentially all there is to the pipelining logic. If you have a clear conceptual understanding of the problem, it’s not that hard. As an inexperienced hardware engineer, it took me a long time to get there. Over many iterations the idea formed to buffer the output from the previous stage in case of a pipeline stall, but I was not confident this was the right approach. I could not find any discussion of this problem online. Finally, I found the post <a href="https://zipcpu.com/blog/2017/08/14/strategies-for-pipelining.html">“Strategies for pipelining logic”</a>, which gives a conceptually clear explanation, and provided a much-needed sanity check.</p>
<p>Since the whole template might be a bit hard to understand in one go, I will iterate from the simplest possible pipeline stage and add the logic as we go, explaining each step.</p>
<h2>1. Pipelining without stalls</h2>
<p>If we have an application that does not need to implement pipeline stalls, the pipelining logic is blissfully simple. Input goes in, and the next clock cycle the associated output comes out.</p>
<pre><code>entity stage_template is
	port(
		clk: in std_logic;
		input: in previous_stage_output_type;
		output: out stage_output_type := DEFAULT_STAGE_OUTPUT
	);
end stage_template;

architecture Behavioral of stage_template is
	function f(input: previous_stage_output_type) return stage_output_type is
	begin
		-- TODO: implement
	end function;
begin
	process(clk)
	begin
		if rising_edge(clk) then
			output &lt;= f(input);
		end if;
	end process;
end Behavioral;
</code></pre>
<h2>2. Introducing pipeline stalls</h2>
<p>To handle pipeline stalls, we introduce a <code>hold</code> signal. The <code>hold_out</code> signal of a pipeline stage should be connected to the <code>hold_in</code> input of the <em>previous</em> stage, to signal that the current stage is not able to handle any more data.</p>
<p>If the <code>hold_in</code> input of a stage gets raised, the stage should response by “freezing”. The output should not change as long as the <code>hold_in</code> signal is high. This way, the next stage has the time to wait until it is ready to start processing data again. (This could also be done by not “freezing” the output, but it is both easy and faster to keep the output the same – clearing it and setting it again when the <code>hold_in</code> input goes low would cost us an extra cycle.)</p>
<p>The logic still looks pretty simple.</p>
<pre><code>entity stage_template is
	port(
		clk: in std_logic;
		input: in previous_stage_output_type;
		output: out stage_output_type := DEFAULT_STAGE_OUTPUT;
		hold_in: in std_logic;
		hold_out: out std_logic
	);
end stage_template;

architecture Behavioral of stage_template is
	function f(input: previous_stage_output_type) return stage_output_type is
	begin
		-- TODO: implement
	end function;
begin
	process(clk)
	begin
		if rising_edge(clk) then
			if hold_in = '0' then
				output &lt;= f(input);
			end if;

			hold_out &lt;= hold_in;
		end if;
	end process;
end Behavioral;
</code></pre>
<p>This code captures the <em>idea</em> of the <code>hold</code> signal quite well, but don’t try to use this code yet, though.</p>
<h2>3. Making it work</h2>
<p>So, before I said something about buffering input, right? The code from the last section <em>doesn’t actually work</em>: Data will get dropped from the pipeline when a pipeline stall occurs.</p>
<p>As mentioned before, if the <code>hold_in</code> signal from a stage gets raised in clock cycle N, in clock cycle N + 1 the output from the stage before it will get lost. Why? In clock cycle N the previous clock cycle can’t know about the pipeline stall yet, so it will happily output data in clock cycle N + 1, which will than not get accepted by the current stage (because <code>hold_in</code> is already high for that stage).</p>
<p>This change is a bit more complicated. I have added a <code>valid</code> signal to the input. If it is zero, the data is not valid and can be ignored. The input is now chosen from either the buffered input or the actual input to the stage; if <code>buffered_input</code> is valid, the variable <code>v_input</code> is taken as the input, otherwise the input to the stage (e.g. the <code>input</code> signal) is taken as the input.</p>
<p>The <code>buffered_input</code> register is cleared if <code>hold_in</code> is low. This works because in this case the input will be selected from <code>buffered_input</code>, and in the next clock cycle the output will be generated from <code>buffered_input</code>, so it’s no longer needed.</p>
<p>Another trick that is applied is that the <code>hold_out</code> signal is raised if and only if <code>buffered_input</code> holds a valid instruction. This way, the flow of data into the stage will keep going until the stage can’t process any more data, instead of just completely locking up one cycle after the <code>hold_in</code> signal is raised.</p>
<pre><code>entity stage_template is
	port(
		clk: in std_logic;
		input: in previous_stage_output_type;
		output: out stage_output_type := DEFAULT_STAGE_OUTPUT;
		hold_in: in std_logic;
		hold_out: out std_logic
	);
end stage_template;


architecture Behavioral of stage_template is
	signal buffered_input: previous_stage_output_type := DEFAULT_PREVIOUS_STAGE_OUTPUT;

	function f(input: previous_stage_output_type) return stage_output_type is
	begin
		if input.valid = '0' then
			return DEFAULT_STAGE_OUTPUT;
		end if;

		-- TODO: implement
	end function;
begin
	hold_out &lt;= buffered_input.valid;

	process(clk)
		variable v_input: previous_stage_output_type;
	begin
		if rising_edge(clk) then
			if buffered_input.valid = '1' then
				v_input := buffered_input;
			else
				v_input := input;
			end if;

			if hold_in = '0' then
				output &lt;= f(v_input);
				buffered_input &lt;= DEFAULT_PREVIOUS_STAGE_OUTPUT;
			else
				buffered_input &lt;= v_input;
			end if;
		end if;
	end process;
end Behavioral;
</code></pre>
<h2>4. Generating a pipeline stall</h2>
<p>The code in the previous section works fine, but the template there only passes on a <code>hold</code> signal. It doesn’t cover how to <em>generate</em> pipeline stalls.</p>
<p>For this, we introduce a variable <code>v_stall</code> that is generated from the input with a function <code>should_stall</code>. If the stage stalls, it should not output any data, so the output is set to <code>DEFAULT_STAGE_OUTPUT</code>, which is has the <code>valid</code> flag set to <code>0</code>.</p>
<pre><code>entity stage_template is
	port(
		clk: in std_logic;
		input: in previous_stage_output_type;
		output: out stage_output_type := DEFAULT_STAGE_OUTPUT;
		hold_in: in std_logic;
		hold_out: out std_logic
	);
end stage_template;

architecture Behavioral of stage_template is
	signal buffered_input: previous_stage_output_type := DEFAULT_PREVIOUS_STAGE_OUTPUT;

	function should_stall(input: previous_stage_output_type) return boolean is
	begin
		-- TODO: implement
	end function;

	function f(input: previous_stage_output_type) return stage_output_type is
	begin
		if input.valid = '0' then
			return DEFAULT_STAGE_OUTPUT;
		end if;

		-- TODO: implement
	end function;
begin
	hold_out &lt;= buffered_input.valid;

	process(clk)
		variable v_input: previous_stage_output_type;
		variable v_should_stall: boolean;
	begin
		if rising_edge(clk) then
			if buffered_input.valid = '1' then
				v_input := buffered_input;
			else
				v_input := input;
			end if;

			if hold_in = '0' then
				v_should_stall := should_stall(v_input)
				if v_should_stall then
					output &lt;= DEFAULT_STAGE_OUTPUT;
				end if;
			end if;

			if hold_in = '0' and not(v_should_stall) then
				output &lt;= f(v_input);
				buffered_input &lt;= DEFAULT_PREVIOUS_STAGE_OUTPUT;
			else
				buffered_input &lt;= v_input;
			end if;
		end if;
	end process;
end Behavioral;
</code></pre>
<p>This finally is a template that is sufficient for most stages.</p>
<h2>5. Adding transient output</h2>
<p>Some stages need to communicate with a component that is not part of the pipeline. Those components outside of the pipeline are not subject to the pipelining logic, and will interpret data that is frozen for a few cycles as multiple occurances of the same data.</p>
<p>To avoid this, we can add a <em>transient</em> output which is held at most one cycle. This is very similar to the normal output, except that it gets reset whenever <code>hold_in</code> is high.</p>
<p>It’s a bit of a stretch to add this since this is usually not needed, but I’ll put it in here anyway.</p>
<pre><code>entity stage_template is
	port(
		clk: in std_logic;
		input: in previous_stage_output_type;
		output: out stage_output_type := DEFAULT_STAGE_OUTPUT;
		transient_output: out stage_transient_output_type := DEFAULT_STAGE_TRANSIENT_OUTPUT;
		hold_in: in std_logic;
		hold_out: out std_logic
	);
end stage_template;

architecture Behavioral of stage_template is
	signal buffered_input: previous_stage_output_type := DEFAULT_PREVIOUS_STAGE_OUTPUT;

	function should_stall(input: previous_stage_output_type) return boolean is
	begin
		-- TODO: implement
	end function;

	function f(input: previous_stage_output_type) return stage_output_type is
	begin
		if input.valid = '0' then
			return DEFAULT_STAGE_OUTPUT;
		end if;

		-- TODO: implement
	end function;

	function g(input: previous_stage_output_type) return stage_transient_output_type is
	begin
		if input.valid = '0' then
			return DEFAULT_STAGE_TRANSIENT_OUTPUT;
		end if;

		-- TODO: implement
	end function;
begin
	hold_out &lt;= buffered_input.valid;

	process(clk)
		variable v_input: previous_stage_output_type;
		variable v_should_stall: boolean;
	begin
		if rising_edge(clk) then
			if buffered_input.valid = '1' then
				v_input := buffered_input;
			else
				v_input := input;
			end if;

			if hold_in = '0' then
				v_should_stall := should_stall(v_input)
				if v_should_stall then
					output &lt;= DEFAULT_STAGE_OUTPUT;
				end if;
			end if;

			if hold_in = '0' and not(v_should_stall) then
				output &lt;= f(v_input);
				transient_output &lt;= g(v_input);
				buffered_input &lt;= DEFAULT_PREVIOUS_STAGE_OUTPUT;
			else
				transient_output &lt;= DEFAULT_STAGE_TRANSIENT_OUTPUT;
				buffered_input &lt;= v_input;
			end if;
		end if;
	end process;
end Behavioral;
</code></pre>
</article>
</body>
</html>
